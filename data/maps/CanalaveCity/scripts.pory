const PLAYER = OBJ_EVENT_ID_PLAYER
const RUPERT = 1
const BEN = 4
const GRUNT_1 = 5
const GRUNT_2 = 2
const GRUNT_3 = 3
const STUNKY = 6
const WINGULL = 7
const BARRY = 8
const PRINPLUP = 9

mapscripts CanalaveCity_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_CANALAVE_STATE, 0 {
            hideobjectat(PLAYER, MAP_CANALAVE_CITY)
            hideobjectat(RUPERT, MAP_CANALAVE_CITY)
            hideobjectat(BARRY, MAP_CANALAVE_CITY)
            hideobjectat(PRINPLUP, MAP_CANALAVE_CITY)
            setvar(VAR_CANALAVE_STATE, 1)
        }
        VAR_CANALAVE_STATE, 2 {
            hideobjectat(PLAYER, MAP_CANALAVE_CITY)
            hideobjectat(RUPERT, MAP_CANALAVE_CITY)
            hideobjectat(BEN, MAP_CANALAVE_CITY)
            hideobjectat(STUNKY, MAP_CANALAVE_CITY)
            hideobjectat(WINGULL, MAP_CANALAVE_CITY)
            
            removeobject(GRUNT_1, MAP_CANALAVE_CITY)
            setobjectxyperm(GRUNT_1, 19, 13)
            //setobjectmovementtype(GRUNT_1, MOVEMENT_TYPE_FACE_LEFT)
            addobject(GRUNT_1, MAP_CANALAVE_CITY)
            
            removeobject(GRUNT_2, MAP_CANALAVE_CITY)
            setobjectxyperm(GRUNT_2, 18, 11)
            //setobjectmovementtype(GRUNT_2, MOVEMENT_TYPE_LOOK_AROUND)
            addobject(GRUNT_2, MAP_CANALAVE_CITY)
            
            removeobject(GRUNT_3, MAP_CANALAVE_CITY)
            setobjectxyperm(GRUNT_3, 16, 15)
            //setobjectmovementtype(GRUNT_3, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
            addobject(GRUNT_3, MAP_CANALAVE_CITY)
            
            setvar(VAR_CANALAVE_STATE, 3)
        }
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CANALAVE_STATE, 1: CanalaveCity_BeginStartingCutscene
        VAR_CANALAVE_STATE, 3: CanalaveCity_BarrySavesTheDay
    ]
}

script CanalaveCity_BeginStartingCutscene {
    opendoor(15, 10)
    waitdooranim
    showobjectat(RUPERT, MAP_CANALAVE_CITY)
    applymovement(RUPERT, Common_Movement_WalkDown)
    waitmovement(RUPERT)
    closedoor(15, 10)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}My poor son{PAUSE 4}.{PAUSE 4}.{PAUSE 4}.{PAUSE 8}\nYou'll get better, {PAUSE 4}I swear."), MSGBOX_DEFAULT)
    closemessage
    applymovement(PLAYER, WalkOutOfHouseCamera)
    applymovement(RUPERT, WalkOutOfHouseRupert)
    waitmovement(RUPERT)
    applymovement(PLAYER, WalkOutOfHouseCameraEnd)
    applymovement(RUPERT, WalkOutOfHouseRupertEnd)
    waitmovement(PLAYER)
    delay(16)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}Ben! {PAUSE 4}What's going on!"), MSGBOX_DEFAULT)
    closemessage
    applymovement(PLAYER, MoveToGrunts)
    waitmovement(PLAYER)
    applymovement(BEN, Common_Movement_ExclamationMark)
    delay(16)
    msgbox(format("{COLOR BLUE}Ben: {COLOR DARK_GREY}Get back inside Rupert!\nThey're trying to take the dock!"), MSGBOX_DEFAULT)
    closemessage
    applymovement(GRUNT_1, ObjectGetAttentionDown)
    waitmovement(GRUNT_1)
    delay(4)
    msgbox(format("{COLOR RED}Grunt 1: {COLOR DARK_GREY}Damn right we are! And you can't do anything to stop us."), MSGBOX_DEFAULT)
    closemessage
    applymovement(GRUNT_2, ObjectGetAttentionDown)
    waitmovement(GRUNT_2)
    delay(4)
    msgbox(format("{COLOR RED}Grunt 2: {COLOR DARK_GREY}Give us the key to the boat already, {PAUSE 8}old man."), MSGBOX_DEFAULT)
    delay(16)
    msgbox(format("{COLOR BLUE}Ben: {COLOR DARK_GREY}In your dreams! {PAUSE 8}Rupert, get inside and lock the door, now!"), MSGBOX_DEFAULT)
    closemessage
    applymovement(GRUNT_3, ObjectGetAttentionDown)
    waitmovement(GRUNT_3)
    delay(4)
    msgbox(format("{COLOR RED}Grunt 3: {COLOR DARK_GREY}Enough of this! \n{PAUSE 8}Take him out and let's get the job done."), MSGBOX_DEFAULT)
    delay(16)
    msgbox(format("{COLOR RED}Grunt 1: {COLOR DARK_GREY}Right{PAUSE 4}.{PAUSE 4}.{PAUSE 4}.\pStunky! {PAUSE 4}Use Feint attack!"), MSGBOX_DEFAULT)
    delay(16)
    msgbox(format("{COLOR RED}Stunky: {COLOR DARK_GREY}Stuunk!"), MSGBOX_DEFAULT)
    applymovement(STUNKY, FeintAttackStart)
    delay(16)
    message(format("{COLOR BLUE}Ben: {COLOR DARK_GREY}Rupert, go!"))
    waitmovement(STUNKY)
    applymovement(WINGULL, WingullSurprised)
    applymovement(STUNKY, FeintAttackEnd)
    waitmovement(STUNKY)
    setobjectnewmovementtype(WINGULL, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
    setobjectnewmovementtype(STUNKY, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    waitmessage
    waitbuttonpress
    closemessage
    applymovement(RUPERT, Common_Movement_ExclamationMark)
    applymovement(PLAYER, MoveToRupert)
    waitmovement(PLAYER)
    applymovement(PLAYER, WalkIntoHouse)
    applymovement(RUPERT, WalkIntoHouse)
    waitmovement(PLAYER)
    opendoor(15, 10)
    waitdooranim
    showobjectat(RUPERT, MAP_CANALAVE_CITY)
    applymovement(RUPERT, Common_Movement_WalkUp)
    waitmovement(RUPERT)
    closedoor(15, 10)
    hideobjectat(RUPERT, MAP_CANALAVE_CITY)
    setvar(VAR_CANALAVE_STATE, 2)
    warp(MAP_THE_VOID, -1, 15, 12)
    end
}

movement WalkOutOfHouseRupert {
    walk_down
    walk_left * 5
    face_up
    delay_16
    delay_8
    emote_exclamation_mark
    delay_8
}

movement WalkOutOfHouseRupertEnd {
    walk_fast_up * 2
}

movement WalkOutOfHouseCamera {
    walk_down
    walk_left * 5
    face_up
    
}

movement WalkOutOfHouseCameraEnd {
    walk_fast_up * 3
}

movement MoveToGrunts {
    walk_fast_up * 4
}

movement ObjectGetAttentionDown {
    jump_in_place_down * 2
    delay_16
}

movement ObjectGetAttentionLeft {
    jump_in_place_left * 2
    delay_16
}

movement FeintAttackStart {
    jump_in_place_down * 2
    jump_down
}

movement WingullSurprised {
    jump_in_place_up
}

movement FeintAttackEnd {
    jump_up_backwards
}

movement MoveToRupert {
    walk_fast_down * 5
}

movement WalkIntoHouse {
    walk_down_backwards * 2
    walk_fast_right * 5
    walk_fast_up * 1
}

script CanalaveCity_BarrySavesTheDay {
    lockall
    //Move everything down
    applymovement(PLAYER, BarryWalkFromGym)
    applymovement(BARRY, BarryWalkFromGym)
    applymovement(PRINPLUP, BarryWalkFromGym)
    delay(16)
    //Start Byron strong message. It starts while walking.
    message(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Oh man, Byron is so strong{PAUSE 4}.{PAUSE 4}.{PAUSE 4}.{PAUSE 4}\nHe beat us without breaking a sweat!"))
    waitmovement(PLAYER)
    applymovement(PRINPLUP, PrinplupFaceBarry)
    waitmessage
    waitbuttonpress
    closemessage
    //End Byron message
    //Move Barry to left
    applymovement(PLAYER, BarryShortLeft)
    applymovement(BARRY, BarryShortLeft)
    waitmovement(PLAYER)
    delay(16)
    //Mini speech
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}We sure can't can beat him yet, {PAUSE 8}\nI'm still a long way from his level."), MSGBOX_DEFAULT)
    applymovement(BARRY, ObjectRealizationDown)
    delay(16)
    msgbox(format("But I'm not giving up! {PAUSE 4}No sir!\n {PAUSE 8}We're gonna train together!\p That's right! Even harder than before! {PAUSE 8}Hear that Prinplup?"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    //Prinplup jumps while it says it's cry
    message(format("{COLOR BLUE}Prinplup: {COLOR DARK_GREY}Prinp! Prinp!"))
    applymovement(PRINPLUP, ObjectGetAttentionLeft)
    waitmovement(PRINPLUP)
    waitmessage
    waitbuttonpress
    closemessage
    delay(16)
    //Ask Prinplup for advice while moving
    message(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}We still need to find where to train though. {PAUSE 8}What do you think?"))
    applymovement(PLAYER, BarryShortRight)
    applymovement(BARRY, BarryShortRight)
    waitmovement(PLAYER)
    applymovement(PLAYER, BarryShortRight)
    applymovement(BARRY, BarryShortRight)
    turnobject(PRINPLUP, DIR_NORTH)
    waitmovement(PLAYER)
    turnobject(BARRY, DIR_SOUTH)
    waitmessage
    waitbuttonpress
    closemessage
    //End movement and Prinplup doesn't speak
    delay(16)
    message(format("{COLOR BLUE}Prinplup: {COLOR DARK_GREY}{PAUSE 8}.{PAUSE 32}.{PAUSE 32}.{PAUSE 32}"))
    waitmessage
    waitbuttonpress
    closemessage
    delay(32)
    //Prinplup doesn't speak, Barry moves away
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Right. {PAUSE 32}\nI keep forgetting you can't speak."), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    applymovement(PLAYER, BarryShortLeft)
    applymovement(BARRY, BarryShortLeft)
    waitmovement(PLAYER)
    turnobject(PRINPLUP, DIR_WEST)
    delay(24)
    //Think and surprise Prinplup
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Hm{PAUSE 4}.{PAUSE 4}.{PAUSE 4}.{PAUSE 4}"), MSGBOX_DEFAULT)
    closemessage
    applymovement(BARRY, ObjectRealizationDownExclamation)
    delay(16)
    message(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Oh, {PAUSE 2}I know just the place! \pCome on Prinplup, we're going to Iron Island!"))
    delay(4)
    applymovement(PRINPLUP, ObjectRealizationLeftExclamation)
    waitmovement(PRINPLUP)
    waitmessage
    waitbuttonpress
    closemessage
    //End surprise, start moving to dock
    delay(16)
    applymovement(PLAYER, Common_Movement_WalkDown2)
    applymovement(BARRY, Common_Movement_WalkDown2)
    waitmovement(PLAYER)
    turnobject(PRINPLUP, DIR_SOUTH)
    //Prinplup doesn't want to move
    delay(8)
    applymovement(PRINPLUP, Common_Movement_QuestionMark)
    delay(32)
    turnobject(BARRY, DIR_NORTH)
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Why that face buddy?"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    applymovement(PRINPLUP, ObjectRealizationDown)
    waitmovement(PRINPLUP)
    delay(16)
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}You still can't talk you know{PAUSE 4}.{PAUSE 4}.{PAUSE 4}.{PAUSE 4}"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    applymovement(PRINPLUP, ObjectGetAttentionDown)
    waitmovement(PRINPLUP)
    delay(16)
    msgbox(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}Gotta go to the docks to get to Iron Island! Come on!"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    //Prinplup catches up to Barry, Barry moves to dock
    applymovement(PRINPLUP, PrinplupStartDock)
    waitmovement(PRINPLUP)
    applymovement(PLAYER, Common_Movement_WalkDown4)
    applymovement(BARRY, Common_Movement_WalkDown4)
    applymovement(PRINPLUP, PrinplupTurnDock)
    waitmovement(PLAYER)
    applymovement(PLAYER, Common_Movement_WalkRight)
    applymovement(BARRY, Common_Movement_WalkRight)
    waitmovement(PLAYER)
    applymovement(PLAYER, Common_Movement_WalkRight2)
    applymovement(BARRY, Common_Movement_WalkRight2)
    applymovement(PRINPLUP, Common_Movement_WalkRight2)
    waitmovement(PLAYER)
    //Barry sees Team Galactic
    applymovement(PLAYER, BarryShortRight)
    applymovement(BARRY, BarryShortRight)
    applymovement(PRINPLUP, BarryShortRight)
    waitmovement(PLAYER)
    delay(16)
    message(format("{COLOR BLUE}Barry: {COLOR DARK_GREY}What is Team Galactic doing around here? \pWell, hopefully they aren't up to anything bad."))
    delay(8)
    applymovement(BARRY, Common_Movement_QuestionMark)
    delay(32)
    waitmessage
    waitbuttonpress
    closemessage
    applymovement(PLAYER, Common_Movement_WalkRight2)
    applymovement(BARRY, Common_Movement_WalkRight2)
    applymovement(PRINPLUP, Common_Movement_WalkRight2)
    waitmovement(PLAYER)
    setvar(VAR_CANALAVE_STATE, 4)
    releaseall
    end
}

movement BarryWalkFromGym {
    walk_slow_down * 2
}

movement PrinplupFaceBarry {
    walk_down
    face_left
}

movement BarryShortLeft {
    walk_slow_left
}

movement BarryShortRight {
    walk_slow_right
}

movement ObjectRealizationDown{
    jump_in_place_down
}

movement ObjectRealizationDownExclamation{
    jump_in_place_down
    emote_exclamation_mark
}

movement ObjectRealizationLeftExclamation{
    jump_in_place_left
    emote_exclamation_mark
}

movement PrinplupStartDock {
    jump_in_place_down * 2
    delay_16
    walk_down
}

movement PrinplupTurnDock {
    walk_down * 2
    walk_slow_down
    face_right
}