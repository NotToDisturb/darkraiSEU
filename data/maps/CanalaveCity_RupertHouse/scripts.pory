const PLAYER = OBJ_EVENT_ID_PLAYER
const CHILD = 1
const RUPERT = 2

mapscripts CanalaveCity_RupertHouse_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_CANALAVE_STATE, 0 {
            hideobjectat(PLAYER, MAP_CANALAVE_CITY_RUPERT_HOUSE)
            setvar(VAR_CANALAVE_STATE, 1)
        }
        VAR_CANALAVE_STATE, 4 {
            hideobjectat(PLAYER, MAP_CANALAVE_CITY_RUPERT_HOUSE)
            
            removeobject(RUPERT, MAP_CANALAVE_CITY_RUPERT_HOUSE)
            setobjectxyperm(RUPERT, 5, 14)
            addobject(RUPERT, MAP_CANALAVE_CITY_RUPERT_HOUSE)
            
            turnobject(RUPERT, DIR_NORTH)
            setvar(VAR_CANALAVE_STATE, 5)
        }
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CANALAVE_STATE, 1: CanalaveCity_RupertSonIntro
        VAR_CANALAVE_STATE, 5: CanalaveCity_RupertSonToVoid
    ]
}

script CanalaveCity_RupertSonIntro {
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}He's been sleeping for two entire days{PAUSE 4}.{PAUSE 8}.{PAUSE 8}.{PAUSE 8}\pMy boy, what is happening to you."), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    msgbox(format("{COLOR BLUE}Child: {COLOR DARK_GREY}{PAUSE 8}.{PAUSE 32}.{PAUSE 32}.{PAUSE 32}"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}I have to go get help,{PAUSE 8} surely Nurse Joy will know what to do."), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    applymovement(PLAYER, Common_Movement_WalkLeft)
    waitmovement(PLAYER)
    applymovement(RUPERT, Common_Movement_WalkLeft4)
    applymovement(PLAYER, Common_Movement_WalkLeft4)
    waitmovement(PLAYER)
    delay(8)
    turnobject(RUPERT, DIR_EAST)
    delay(16)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}You hold on now, okay?"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    applymovement(PLAYER, Common_Movement_WalkLeft4)
    applymovement(RUPERT, Common_Movement_WalkLeft4)
    waitmovement(PLAYER)
    applymovement(PLAYER, Common_Movement_WalkDown4)
    applymovement(RUPERT, Common_Movement_WalkDown4)
    waitmovement(PLAYER)
    delay(16)
    removeobject(RUPERT, MAP_CANALAVE_CITY_RUPERT_HOUSE)
    delay(16)
    setvar(VAR_CANALAVE_STATE, 2)
    warp(MAP_CANALAVE_CITY, -1, 15, 11)
}

script CanalaveCity_RupertSonToVoid {
    delay(8)
    applymovement(PLAYER, Common_Movement_WalkUp4)
    applymovement(RUPERT, Common_Movement_WalkUp4)
    waitmovement(PLAYER)
    delay(8)
    turnobject(RUPERT, DIR_SOUTH)
    delay(8)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}Damned Team Galactic!\n Bunch of crazy bastards!"), MSGBOX_DEFAULT)
    closemessage
    waitmovement(PLAYER)
    applymovement(PLAYER, Common_Movement_WalkRight4)
    applymovement(RUPERT, Common_Movement_WalkRight4)
    waitmovement(RUPERT)
    message(format("{COLOR BLUE}Child: {COLOR DARK_GREY}He{PAUSE 8}.{PAUSE 16}.{PAUSE 16}.{PAUSE 16}\pIs{PAUSE 8}.{PAUSE 16}.{PAUSE 16}.{PAUSE 16}\pWatching{PAUSE 8}.{PAUSE 16}.{PAUSE 16}.{PAUSE 16}"))
    delay(48)
    applymovement(RUPERT, Common_Movement_ExclamationMark)
    waitmovement(RUPERT)
    applymovement(PLAYER, CameraRunToSon)
    applymovement(RUPERT, RupertRunToSon)
    waitmovement(PLAYER)
    waitmessage
    waitbuttonpress
    closemessage
    delay(16)
    msgbox(format("{COLOR BLUE}Rupert: {COLOR DARK_GREY}Who is watching son? Who is it?"), MSGBOX_DEFAULT)
    closemessage
    delay(16)
    msgbox(format("{COLOR BLUE}Child: {COLOR DARK_GREY}Dar{PAUSE 8}.{PAUSE 16}.{PAUSE 16}.{PAUSE 32}\nDark{PAUSE 8}.{PAUSE 16}.{PAUSE 16}.{PAUSE 16}"), MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_CANALAVE_STATE, 6)
    warp(MAP_THE_VOID, -1, 15, 12)
}

movement RupertRunToSon{
    walk_fast_right * 4
}

movement CameraRunToSon{
    walk_fast_right * 5
}